group 'com.instamotor'
version '1.3.0'

buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'maven'

allprojects {
    apply plugin: 'de.fuerstenau.buildconfig'
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    buildConfig {
        appName = rootProject.name
        version = rootProject.version
        packageName = rootProject.group
        clsName = 'BuildConfig'
        buildConfigField 'String[]', 'TESTED_GRADLE_VERSIONS', '{"4.9", "5.6.4", "6.0"}'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceSets {
        main {
            java {
                srcDirs += "${buildDir.absolutePath}/gen/buildconfig/src/main"
            }
        }
    }
}

def gitTag() {
    def tag = 'git tag --list --points-at HEAD'.execute((List) null, rootProject.projectDir).text.trim()

    if (tag.split(System.lineSeparator()).length > 1) {
        throw new IllegalStateException("gitTag is accessed but commit has multiple tags: $tag")
    }

    return tag
}

def validateTagAndVersion() {
    if (gitTag().isEmpty()) {
        throw new IllegalStateException('Publishing is not allowed because current commit has no tag')
    }

    if (version != gitTag().substring(1)) {
        throw new IllegalStateException('Publishing is not allowed git tag version != project version')
    }
}
